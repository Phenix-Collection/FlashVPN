apply plugin: 'com.android.application'

def releaseTime() {
    return new Date().format("MMdd", TimeZone.getTimeZone("UTC+8"))
}

def getSignInfo(propFileName) {
    def com.android.build.gradle.internal.dsl.SigningConfig signingConfig = new com.android.build.gradle.internal.dsl.SigningConfig("signInfo")
    File propFile = file(propFileName);
    if (propFile.exists()) {
        def Properties props = new Properties()
        props.load(new FileInputStream(propFile))
        signingConfig.storeFile = file(props['STORE_FILE'])
        signingConfig.storePassword = props['STORE_PASSWORD']
        signingConfig.keyAlias = props['KEY_ALIAS']
        signingConfig.keyPassword = props['KEY_PASSWORD']
        println('signConfig is exits')
    } else {
        println('sign config is null')
        signingConfig = null
    }
    return signingConfig;
}

android {
    compileSdkVersion 26
    buildToolsVersion '27.0.3'
    defaultConfig {
        applicationId "com.polestar.minesweeperclassic"
        minSdkVersion 16
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }
    signingConfigs {
        myConfig {
            storeFile file("..\\publish\\test.keystore")
            storePassword "123456"
            keyAlias "test.keystore"
            keyPassword "123456"
        }
    }

    flavorDimensions("channel")

    productFlavors {
        develop {
            dimension "channel"
            versionCode 9999
            versionName '9999'
            manifestPlaceholders = [CHANNEL_NAME_VALUE: "develop", CHANNEL_TID_VALUE: ""]
        }
        play {
            dimension "channel"
            versionCode 2051
            versionName "2.0.51.${releaseTime()}"
            manifestPlaceholders = [CHANNEL_NAME_VALUE: "play", CHANNEL_TID_VALUE: ""]
        }
    }

    buildTypes {
        debug {
            jniDebuggable false
            multiDexEnabled true
        }
        release {
//            signingConfig signingConfigs.myConfig
            minifyEnabled true
            multiDexEnabled true
            signingConfig getSignInfo("./sign.properties")
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }
}

repositories {
    flatDir {
        dirs 'libs',project(':fuseadloader').file('libs')
    }
    maven {
        url "https://dl.bintray.com/ironsource-mobile/android-sdk"
    }
    maven {
        //IronSource Adapters
        url "http://dl.bintray.com/ironsource-mobile/android-adapters"
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'] )
    implementation 'com.tencent.bugly:crashreport:2.6.5'
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    //firebase messaging/notifications
    implementation 'com.google.firebase:firebase-core:16.0.7'
    implementation 'com.google.firebase:firebase-config:16.3.0'
    implementation 'com.google.firebase:firebase-ads:17.1.3'
    implementation 'com.google.android.exoplayer:exoplayer:r2.4.4'
    implementation 'com.android.support:multidex:1.0.1'
    implementation 'com.facebook.android:audience-network-sdk:5.1.0'
    implementation 'com.android.support:appcompat-v7:25.4.0'

    implementation 'com.ironsource.sdk:mediationsdk:6.8.1@jar'
    //Admob
    implementation 'com.ironsource.adapters:admobadapter:4.3.0@jar'
    //Applovin
    implementation 'com.ironsource.adapters:applovinadapter:4.3.1@jar'

    implementation project(':fuseadloader')

    configurations.all {
        resolutionStrategy.eachDependency { DependencyResolveDetails details ->
            def requested = details.requested
            if (requested.group == 'com.android.support') {
                if (!requested.name.startsWith("multidex")) {
                    details.useVersion '25.4.0'
                }
            }
        }
    }
}
// ADD THIS AT THE BOTTOM
apply plugin: 'com.google.gms.google-services'